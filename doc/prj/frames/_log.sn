	[return cond=const.sn.frm.log]

	[add_frame id=log src=_log visible=false]
	[event key='dom=log:#close' label=*exit global=true]

	[event global=true call=true key=r label=*main]
	[event global=true call=true key=arrowup label=*main]
	[event global=true call=true key=upwheel label=*main]

[let_ml name=dicUI]
	{
		"ja": {
			"screen name": "本文履歴",
			"non-text": {
				"close": "閉じる"
			}
		},
		"es": {
			"screen name": "Historial de texto",
			"non-text": {
				"close": "cerca"
			}
		},
		"en": {
			"screen name": "text history",
			"non-text": {
				"close": "close"
			}
		},
		"cn": {
			"screen name": "文本历史",
			"non-text": {
				"close": "关闭"
			}
		},
		"ct": {
			"screen name": "文本歷史",
			"non-text": {
				"close": "關閉"
			}
		}
	}
[endlet_ml]
	[set_frame id=log var_name=val_ui text=&dicUI]

[return]


;*********************************************************
*main
	[return cond=!isGameState]

	[enable_event enabled=false]
	[enable_event enabled=false layer=mes_sysmenu]
	[clear_event]

	; 最新値で更新
	[set_frame id=log var_name=val_dic text=&const.sn.log.json]
	[set_frame id=log var_name=val_ui_lang text=&lang]
	[let_frame id=log var_name=val2ctrl function=true]
	[set_focus del='dom=log:#log button']
	[set_focus add='dom=log:#log button']

	[frame id=log visible=true]

	&isGameState = false
	[event key=r label=*exit]
	[event key=rightclick label=*exit]
	[event key=meta label=*exit]
	[event key=control label=*exit]
	[event key=delete label=*exit]
	[event key=backspace label=*exit]
	[event key=shift label=*exit]
	[event key=end label=*exit]
	[event key=escape label=*exit]
	[event key=sn:chgNavLang label=*chgNavLang call=true]
[s]


*chgNavLang
	[call label=*exit]

	[call fn=ext_lang]
	[reload_script]

*null
[return]

;*********************************************************
*exit
	[se fn=&sysse_cancel buf=SYS]

	[frame id=log visible=false]

	[enable_event enabled=true]
	[enable_event enabled=true layer=mes_sysmenu]
	[set_focus to=null]
	&isGameState = true
[return]
