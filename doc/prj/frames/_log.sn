	[return cond=const.sn.frm.log]

	[add_frame id=log src=_log visible=false]
	[event key='dom=log:#close' label=*exit global=true]

	[event global=true call=true key=r label=*main]
	[event global=true call=true key=arrowup label=*main]
	[event global=true call=true key=upwheel label=*main]
	[event global=true call=true key=pageup label=*page arg=prev]
	[event global=true call=true key=pagedown label=*page arg=next]
[return]

*page
	[return cond="! isGameState"]
	[page to=&sn.eventArg]
[return]


;*********************************************************
*main
	[return cond="! isGameState"]

	[enable_event enabled=false]
	[enable_event enabled=false layer=mes_sysmenu]
	[clear_event]

	; 最新値で更新
	[set_frame id=log var_name=val_dic text=&const.sn.log.json]
	[let_frame id=log var_name=val2ctrl function=true]
	[set_focus del='dom=log:#log button']
	[set_focus add='dom=log:#log button']

	; 下部からスライドイン
	[frame id=log visible=true y=&const.sn.frm.log.height]
	[tsy_frame id=log time=500 y=0]
	[wait_tsy id=log]

	&isGameState = false
	[event key=r label=*exit]
	[event key=rightclick label=*exit]
	[event key=meta label=*exit]
	[event key=control label=*exit]
	[event key=delete label=*exit]
	[event key=backspace label=*exit]
	[event key=shift label=*exit]
	[event key=end label=*exit]
[s]

*null
[return]

;*********************************************************
*exit
	[se fn=&sysse_cancel buf=SYS]

	[tsy_frame id=log time=500 y=&const.sn.frm.log.height]
	[wait_tsy id=log]
	[frame id=log visible=false]

	[enable_event enabled=true]
	[enable_event enabled=true layer=mes_sysmenu]
	[set_focus to=null]
	&isGameState = true
[return]
