	[return cond=const.sn.frm.config]

	[add_frame id=config src=_config visible=false]
	[event key='dom=config:#close' label=*exit global=true]

	[event global=true call=true key=c label=*rclick_config]
;	[event global=true call=true key=t label=*text_back_alpha_sub]
;	[event global=true call=true key=y label=*text_back_alpha_add]
;	[event global=true call=true key=u label=*text_auto_msecwait_sub]
;	[event global=true call=true key=i label=*text_auto_msecwait_add]

	; バック不透明度
	[event key='dom=config:#sldBackAlpha' label=*sldBackAlpha global=true call=true]
	; 通常文字表示待ち
	[event key='dom=config:#sldChWaitMsec' label=*sldChWaitMsec global=true call=true]
		[event key='dom=config:#chkChDoWait' label=*chkChDoWait global=true call=true]
	[event key='dom=config:#sldAutoLWaitMsec' label=*sldAutoLWaitMsec global=true call=true]
	[event key='dom=config:#sldAutoPWaitMsec' label=*sldAutoPWaitMsec global=true call=true]

	; 既読文字表示待ち
	[event key='dom=config:#sldChWaitMsec_Kidoku' label=*sldChWaitMsec_Kidoku global=true call=true]
		[event key='dom=config:#chkChDoWait_Kidoku' label=*chkChDoWait_Kidoku global=true call=true]
	[event key='dom=config:#sldAutoLWaitMsec_Kidoku' label=*sldAutoLWaitMsec_Kidoku global=true call=true]
	[event key='dom=config:#sldAutoPWaitMsec_Kidoku' label=*sldAutoPWaitMsec_Kidoku global=true call=true]

	; 音量
	[event key='dom=config:#sldVolGlobal' label=*sldVolGlobal global=true call=true]
	[event key='dom=config:#sldVolBgm' label=*sldVolBgm global=true call=true]
	[event key='dom=config:#sldVolVoice' label=*sldVolVoice global=true call=true]
	[event key='dom=config:#sldVolSe' label=*sldVolSe global=true call=true]
	[event key='dom=config:#sldVolSys' label=*sldVolSys global=true call=true]
	[event key='dom=config:#sldVolMulTalking' label=*sldVolMulTalking global=true call=true]

	; 画面モード
	[event key='dom=config:#chkScrMode' label=*chkScrMode global=true call=true]

	; エクスポート・インポート
	[event key='dom=config:#export' label=*export global=true call=true]
	[event key='dom=config:#import' label=*import global=true call=true]
	; 完了イベントを検知
	[event key='sn:exported' label=*exported global=true call=true]
	[event key='sn:imported' label=*imported global=true call=true]

[let_ml name=dicUI]
	{
		"ja": {
			"base": {
				"screen name": "設定",
				"現在ver": "現在 ver",
				"non-text": {
					"close": "閉じる"
				}
			},
			"text": {
				"text-SubTitle": "テキスト",
				"バック不透明度": "バック不透明度",
				"バック不透明度_説明": "文字表示サンプル<br/>テキストウインドウの不透明度を表示します。",
				"通常文字表示待ち": "通常文字表示待ち",
				"通常文字-待つ": "待つ",
				"自動読みクリック待ち": "自動読みクリック待ち",
				"自動読み改ページ待ち": "自動読み改ページ待ち",
				"既読文字表示待ち": "既読文字表示待ち",
				"既読文字-待つ": "待つ",
				"既読自動クリック待ち": "既読自動クリック待ち",
				"既読自動改ページ待ち": "既読自動改ページ待ち"
			},
			"sound": {
				"sound-SubTitle": "音量",
				"全体的": "全体的",
				"ＢＧＭのみ": "ＢＧＭのみ",
				"ボイスのみ": "ボイスのみ",
				"効果音のみ": "効果音のみ",
				"システム音のみ": "システム音のみ",
				"ボイス再生中のBGM音量乗数": "ボイス再生中のBGM音量乗数"
			},
			"system": {
				"system-SubTitle": "システム",
				"screen mode": "画面モード",
				"screen mode-Sub": "全画面か",
				"export-Sub": "プレイデータのバックアップ",
				"import-Sub": "プレイデータのリストア",
				"export": "エクスポート",
				"import": "インポート"
			}
		},
		"es": {
			"base": {
				"screen name": "Configuración",
				"現在ver": "version de app",
				"non-text": {
					"close": "cerca"
				}
			},
			"text": {
				"text-SubTitle": "texto",
				"バック不透明度": "Transparencia de texto",
				"バック不透明度_説明": "Ejemplo de visualización de caracteres<br/>Muestra la opacidad de la ventana de texto.",
				"通常文字表示待ち": "Tiempo de espera de los carácteres",
				"通常文字-待つ": "esperar",
				"自動読みクリック待ち": "Velocidad Click Automático",
				"自動読み改ページ待ち": "Velocidad Transición Automático",
				"既読文字表示待ち": "Tiempo de espera de los carácteres ya vistos",
				"既読文字-待つ": "esperar",
				"既読自動クリック待ち": "Tiempo de espera del modo automático",
				"既読自動改ページ待ち": "Tiempo de espera de transición del modo automático"
			},
			"sound": {
				"sound-SubTitle": "Volumen",
				"全体的": "Total",
				"ＢＧＭのみ": "Música de Fondo",
				"ボイスのみ": "Volumen de voz",
				"効果音のみ": "Efectos de sonido",
				"システム音のみ": "Sonidos del sistema"
			},
			"system": {
				"system-SubTitle": "sistema",
				"screen mode": "Modo de pantalla",
				"screen mode-Sub": "pantalla completa",
				"export-Sub": "Copia de seguridad de datos de reproducción",
				"import-Sub": "Restauración de datos de reproducción",
				"export": "exportar",
				"import": "importar"
			}
		},
		"en": {
			"base": {
				"screen name": "Settings",
				"現在ver": "app ver",
				"non-text": {
					"close": "close"
				}
			},
			"text": {
				"text-SubTitle": "Text",
				"バック不透明度": "Text visibility level",
				"バック不透明度_説明": "Character display sample<br/>Shows the opacity of the text window.",
				"通常文字表示待ち": "Text scroll speed",
				"通常文字-待つ": "wait",
				"自動読みクリック待ち": "Auto read click wait time",
				"自動読み改ページ待ち": "Auto read page break wait time",
				"既読文字表示待ち": "Wait time to display already read text",
				"既読文字-待つ": "wait",
				"既読自動クリック待ち": "Auto click wait time for read text",
				"既読自動改ページ待ち": "Auto page break wait time for read text"
			},
			"sound": {
				"sound-SubTitle": "Volume",
				"全体的": "All sounds",
				"ＢＧＭのみ": "BGM",
				"ボイスのみ": "Voice",
				"効果音のみ": "SFX",
				"システム音のみ": "System"
			},
			"system": {
				"system-SubTitle": "System",
				"screen mode": "Screen mode",
				"screen mode-Sub": "full screen",
				"export-Sub": "Play data backup",
				"import-Sub": "Restoring play data",
				"export": "export",
				"import": "import"
			}
		},
		"cn": {
			"base": {
				"screen name": "设定",
				"現在ver": "应用版本",
				"non-text": {
					"close": "关闭"
				}
			},
			"text": {
				"text-SubTitle": "文本",
				"バック不透明度": "背景的不透明度",
				"バック不透明度_説明": "字符显示示例<br/>显示文本窗口的不透明度。",
				"通常文字表示待ち": "通常文字表示待机速度",
				"通常文字-待つ": "等待",
				"自動読みクリック待ち": "自动阅读换行等待速度",
				"自動読み改ページ待ち": "自动阅读换页等待速度",
				"既読文字表示待ち": "已读文字表示等待时间",
				"既読文字-待つ": "等待",
				"既読自動クリック待ち": "已读自动阅读换行等待速度",
				"既読自動改ページ待ち": "已读自动阅读换页等待速度"
			},
			"sound": {
				"sound-SubTitle": "体积",
				"全体的": "整体音量",
				"ＢＧＭのみ": "BGM音量",
				"ボイスのみ": "VOICE的音量",
				"効果音のみ": "音效音量",
				"システム音のみ": "系统音效音量"
			},
			"system": {
				"system-SubTitle": "系统",
				"screen mode": "全屏・视窗模式",
				"screen mode-Sub": "全屏",
				"export-Sub": "播放数据备份",
				"import-Sub": "恢复播放数据",
				"export": "出口",
				"import": "进口"
			}
		},
		"ct": {
			"base": {
				"screen name": "遊戲設定",
				"現在ver": "應用版本",
				"non-text": {
					"close": "關閉"
				}
			},
			"text": {
				"text-SubTitle": "文本",
				"バック不透明度": "背景的不透明度",
				"バック不透明度_説明": "字符顯示示例<br/>顯示文本窗口的不透明度。",
				"通常文字表示待ち": "通常文字表示待機速度",
				"通常文字-待つ": "等待",
				"自動読みクリック待ち": "自動閱讀換行等待速度",
				"自動読み改ページ待ち": "自動閱讀換頁等待速度",
				"既読文字表示待ち": "已讀文字表示等待時間",
				"既読文字-待つ": "等待",
				"既読自動クリック待ち": "已讀自動閱讀換行等待速度",
				"既読自動改ページ待ち": "已讀自動閱讀換頁等待速度"
			},
			"sound": {
				"sound-SubTitle": "體積",
				"全体的": "整體音量",
				"ＢＧＭのみ": "BGM的音量",
				"ボイスのみ": "VOICE的音量",
				"効果音のみ": "音效的音量",
				"システム音のみ": "系統音效的音量"
			},
			"system": {
				"system-SubTitle": "系統",
				"screen mode": "全螢幕模式",
				"screen mode-Sub": "全屏",
				"export-Sub": "播放數據備份",
				"import-Sub": "恢復播放數據",
				"export": "出口",
				"import": "進口"
			}
		}
	}
[endlet_ml]
	[set_frame id=config var_name=val_ui text=&dicUI]

[return]


;*********************************************************
; バック不透明度
*sldBackAlpha
	[let_frame id=config var_name=val_sldBackAlpha]
	&sys:TextLayer.Back.Alpha = const.sn.frm.config.val_sldBackAlpha/100
[return]

; 通常文字表示待ち
*sldChWaitMsec
	[let_frame id=config var_name=val_sldChWaitMsec]
	&sys:sn.tagCh.msecWait = const.sn.frm.config.val_sldChWaitMsec
[return]
*chkChDoWait
		[let_frame id=config var_name=val_chkChDoWait]
		&sys:sn.tagCh.doWait = const.sn.frm.config.val_chkChDoWait
[return]
*sldAutoLWaitMsec
	[let_frame id=config var_name=val_sldAutoLWaitMsec]
	&sys:sn.auto.msecLineWait = const.sn.frm.config.val_sldAutoLWaitMsec
[return]
*sldAutoPWaitMsec
	[let_frame id=config var_name=val_sldAutoPWaitMsec]
	&sys:sn.auto.msecPageWait = const.sn.frm.config.val_sldAutoPWaitMsec
[return]


; 既読文字表示待ち
*sldChWaitMsec_Kidoku
	[let_frame id=config var_name=val_sldChWaitMsec_Kidoku]
	&sys:sn.tagCh.msecWait_Kidoku = const.sn.frm.config.val_sldChWaitMsec_Kidoku
[return]
*chkChDoWait_Kidoku
		[let_frame id=config var_name=val_chkChDoWait_Kidoku]
		&sys:sn.tagCh.doWait_Kidoku = const.sn.frm.config.val_chkChDoWait_Kidoku
[return]
*sldAutoLWaitMsec_Kidoku
	[let_frame id=config var_name=val_sldAutoLWaitMsec_Kidoku]
	&sys:sn.auto.msecLineWait_Kidoku = const.sn.frm.config.val_sldAutoLWaitMsec_Kidoku
[return]
*sldAutoPWaitMsec_Kidoku
	[let_frame id=config var_name=val_sldAutoPWaitMsec_Kidoku]
	&sys:sn.auto.msecPageWait_Kidoku = const.sn.frm.config.val_sldAutoPWaitMsec_Kidoku
[return]


; 音量
*sldVolGlobal
	[let_frame id=config var_name=val_sldVolGlobal]
	&sys:sn.sound.global_volume = const.sn.frm.config.val_sldVolGlobal /100
[return]
*sldVolBgm
	[let_frame id=config var_name=val_sldVolBgm]
	[volume buf=BGM volume=&const.sn.frm.config.val_sldVolBgm/100]
[return]
*sldVolVoice
	[let_frame id=config var_name=val_sldVolVoice]
	[volume buf=VOICE volume=&const.sn.frm.config.val_sldVolVoice/100]
[return]
*sldVolSe
	[let_frame id=config var_name=val_sldVolSe]
	[volume buf=SE volume=&const.sn.frm.config.val_sldVolSe/100]
[return]
*sldVolSys
	[let_frame id=config var_name=val_sldVolSys]
	[volume buf=SYS volume=&const.sn.frm.config.val_sldVolSys/100]
*sldVolMulTalking
	[let_frame id=config var_name=val_sldVolMulTalking]
	&sys:sn.sound.BGM.vol_mul_talking = const.sn.frm.config.val_sldVolMulTalking
[return]


; 画面モード
*chkScrMode
	[let_frame id=config var_name=val_chkScrMode]
	[call fn=main label=*toggle_full_screen cond=const.sn.frm.config.val_chkScrMode!=const.sn.displayState]
[return]


;*********************************************************
*text_back_alpha_sub
	[return cond=!isGameState]
	&alpha = sys:TextLayer.Back.Alpha *100 -5 = int
	[let name=alpha text=0 cond="alpha < 0"]
	&sys:TextLayer.Back.Alpha = alpha /100
[jump label=*text_back_alpha]

*text_back_alpha_add
	[return cond=!isGameState]
	&alpha = sys:TextLayer.Back.Alpha *100 +5 = int
	[let name=alpha text=100 cond="alpha > 100"]
	&sys:TextLayer.Back.Alpha = alpha /100

*text_back_alpha
	[notice text="&mes.バック不透明度 + alpha + '%'"]
	[return]

;*********************************************************
*text_auto_msecwait_sub
	[return cond=!isGameState]
	&wait = sys:sn.auto.msecLineWait -100 = uint
	[let name=wait text=0 cond="wait < 0"]
[jump label=*text_auto_msecwait]

*text_auto_msecwait_add
	[return cond=!isGameState]
	&wait = sys:sn.auto.msecLineWait +100 = uint
	[let name=wait text=3500 cond="wait > 3500"]
*text_auto_msecwait
	&sys:sn.auto.msecLineWait = wait
	[notice text="&mes.自動読み進み速度 + wait + ' msec'"]
[return]


;*********************************************************
*title_config
	[return cond=const.sn.frm.config.visible]
[jump label=*main]

;*********************************************************
*rclick_config
	[return cond=!isGameState]
	[return cond=const.sn.frm.config.visible]
	[se fn=&sysse_ok1 buf=SYS]

;*********************************************************
*main
	&tmpGameState = isGameState

	[enable_event enabled=false]
	[enable_event enabled=false layer=mes_sysmenu]
	&isGameState = false
	&save:sn.doRecLog = false

	[clear_event]

	; 最新値で更新
	[call label=*val2ctrl]

	[frame id=config visible=true]

	[event key=c label=*exit]
	[event key=rightclick label=*exit]
	[event key=meta label=*exit]
	[event key=control label=*exit]
	[event key=delete label=*exit]
	[event key=backspace label=*exit]
	[event key=shift label=*exit]
	[event key=end label=*exit]
	[event key=escape label=*exit]
	[event key=sn:chgNavLang label=*chgNavLang call=true]
	[set_focus to=null]
[s]

*chgNavLang
	[call label=*exit cond="呼び元=='Text'"]

	[call fn=ext_lang]
	[reload_script cond="呼び元=='Text'"]

	[set_frame id=config var_name=val_ui_lang text=&lang]
	[let_frame id=config var_name=ui_redraw function=true]
[return]

; 最新値で更新
*val2ctrl
	; アプリバージョン
	[set_frame id=config var_name=val_version text=&const.sn.config.book.version@str]

	; バック不透明度
	[set_frame id=config var_name=val_sldBackAlpha text=&sys:TextLayer.Back.Alpha*100]
	; 通常文字表示待ち
	[set_frame id=config var_name=val_sldChWaitMsec text=&sys:sn.tagCh.msecWait]
	[set_frame id=config var_name=val_chkChDoWait text=&sys:sn.tagCh.doWait]
	[set_frame id=config var_name=val_sldAutoLWaitMsec text=&sys:sn.auto.msecLineWait]
	[set_frame id=config var_name=val_sldAutoPWaitMsec text=&sys:sn.auto.msecPageWait]

	; 既読文字表示待ち
	[set_frame id=config var_name=val_sldChWaitMsec_Kidoku text=&sys:sn.tagCh.msecWait_Kidoku]
	[set_frame id=config var_name=val_chkChDoWait_Kidoku text=&sys:sn.tagCh.doWait_Kidoku]
	[set_frame id=config var_name=val_sldAutoLWaitMsec_Kidoku text=&sys:sn.auto.msecLineWait_Kidoku]
	[set_frame id=config var_name=val_sldAutoPWaitMsec_Kidoku text=&sys:sn.auto.msecPageWait_Kidoku]

	; 音量
	[set_frame id=config var_name=val_sldVolGlobal text=&sys:sn.sound.global_volume*100]
	[set_frame id=config var_name=val_sldVolBgm text=&sys:const.sn.sound.BGM.volume*100]
	[set_frame id=config var_name=val_sldVolVoice text=&sys:const.sn.sound.VOICE.volume*100]
	[set_frame id=config var_name=val_sldVolSe text=&sys:const.sn.sound.SE.volume*100]
	[set_frame id=config var_name=val_sldVolSys text=&sys:const.sn.sound.SYS.volume*100]
	[set_frame id=config var_name=val_sldVolMulTalking text=&sys:sn.sound.BGM.vol_mul_talking|1]

	[set_frame id=config var_name=val_chkScrMode text=&const.sn.displayState]

	[set_frame id=config var_name=val_ui_lang text=&lang]
	[let_frame id=config var_name=val2ctrl function=true]
[return]


;*********************************************************
*exit
	[se fn=&sysse_cancel buf=SYS]

	[frame id=config visible=false]

	[enable_event enabled=true]
	[enable_event enabled=true layer=mes_sysmenu]
	&isGameState = tmpGameState
	&save:sn.doRecLog = tmpGameState

	[set_focus to=null]
[return]

;*********************************************************
*export		[se fn=&sysse_ok1 buf=SYS][export][return]

*exported	[notice text=&mes.プレイデータをエクスポートしました][return]

;*********************************************************
*import		[se fn=&sysse_ok1 buf=SYS][import][return]

*imported
	[call label=*exit]
	[enable_event enabled=false]
	[enable_event enabled=false layer=mes_sysmenu]

	&t = mes.ゲームを再起動する必要・ブラウザリロード
	[let name=t text=&mes.ゲームを再起動する必要があります cond=const.sn.isApp]
	[notice text=&t timeout=0]
[return]
