	[return cond=const.sn.frm.yesno]

	[add_frame id=yesno src=_yesno visible=false]
	[event key='dom=yesno:#ok' label=*y global=true]
	[event key='dom=yesno:#close' label=*cancel global=true]

[macro name=ask_ync
	sum='プレイヤー意志確認\nプレイヤーに「はい」「いいえ」等の二択で確認を行なう'
	detail='プレイヤーが押下した結果をtmp:_yesnoに「y」か「n」で返す。\n\nボタン素材としてファイル名がMnu_btnOK・Mnu_btnCancelの画像ファイルと、\nダイアログ素材としてファイル名が_yesnoの画像ファイルを使用する'

	%mes='String||プレイヤーに質問したい文言'
]
	[enable_event enabled=false]
	[enable_event enabled=false layer=mes_sysmenu]
	[clear_event]

	; 最新値で更新
	[let name=pic text=%pic|'']
	[set_frame id=yesno var_name=val_dic text=#&'{"mes":"$mp:mes","pic":"$pic"}'#]
	[let_frame id=yesno var_name=val2ctrl function=true]

	[frame id=yesno visible=true float=true]

	&_yesno = 'n'

	[event key=y label=*y]
	[event key=n label=*cancel]
	[event key=s label=*cancel]
	[event key=l label=*cancel]
	[event key=rightclick label=*cancel]
	[event key=meta label=*cancel]
	[event key=control label=*cancel]
	[event key=delete label=*cancel]
	[event key=backspace label=*cancel]
	[event key=shift label=*cancel]
	[event key=end label=*cancel]
[s]

*y	&_yesno = 'y'
	[se fn=&sysse_ok2 buf=SYS]
	[jump label=*exit]
*cancel
	[se fn=&sysse_cancel buf=SYS]
*exit
	[frame id=yesno visible=false]

	[enable_event enabled=true]
	[enable_event enabled=true layer=mes_sysmenu]
	[set_focus to=null]
[endmacro]

[return]
