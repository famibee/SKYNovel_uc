	[return cond=const.sn.frm.submenu]

	[add_frame id=submenu src=_submenu visible=false]
	[event key='dom=submenu:#close' label=*cancel global=true]

	[event key='dom=submenu:#save' label=*save global=true]
	[event key='dom=submenu:#load' label=*load global=true]
	[event key='dom=submenu:#log' label=*log global=true]
	[event key='dom=submenu:#config' label=*config global=true]
	[event key='dom=submenu:#hide_text' label=*hide_text global=true]
	[event key='dom=submenu:#cancel' label=*cancel global=true]
	[event key='dom=submenu:#title' label=*title global=true]
	[event key='dom=submenu:#game_end' label=*game_end global=true]

	[event global=true call=true key=rightclick label=*main]
	[event global=true call=true key=shift label=*main]
	[event global=true call=true key=escape label=*main]
[return]

;*********************************************************
*main
	[return cond=!isGameState]

		&v = const.sn.lay.mes.fore.visible
		[lay layer=mes page=fore visible=false]

		[se fn=&sysse_ok1 buf=SYS]
		[frame id=submenu visible=true]
		[let name=sysmenu_v text=&const.sn.lay.mes_sysmenu.fore.visible cond=useSysMenu]
		[sys_menu visible=false]

	[let name="isGameState" text=false]
*retry
	[enable_event enabled=false]
	[clear_event]

	[event key=rightclick label=*cancel]
	[event key=shift label=*cancel]
	[event key=escape label=*cancel]
	[set_focus to=null]
[s]

;*********************************************************
*toggle_skip
	[jump fn=sub label=*toggle_skip]

;*********************************************************
*save
	[se fn=&sysse_ok1 buf=SYS]
	[call label=*exit]
	[let name="isGameState" text=true]
	[jump fn="_archive" label=*rclick_save]

;*********************************************************
*load
	[se fn=&sysse_ok1 buf=SYS]
	[call label=*exit]
	[let name="isGameState" text=true]
	[jump fn="_archive" label=*rclick_load]

;*********************************************************
*config
	[se fn=&sysse_ok1 buf=SYS]
	[call label=*exit]
	[let name="isGameState" text=true]
	[jump fn="_config" label=*rclick_config]

;*********************************************************
*hide_text
	[se fn=&sysse_ok1 buf=SYS]
	[call label=*exit]
	[let name="isGameState" text=true]
	[jump fn="_hidetext" label=*main]

;*********************************************************
*log
	[se fn=&sysse_ok1 buf=SYS]
	[call label=*exit]
	[let name="isGameState" text=true]
	[jump fn="_log" label=*main]

;*********************************************************
*title
	[ask_ync mes="タイトルに戻りますか？"]
		;#FONT	タイトルに戻りますか？
	[jump label=*retry cond="_yesno=='n'"]

	[call label=*exit]

	[fadeoutse buf=音声 time=1000]
	; テキストレイヤ全画面設定
	[txt_lay_fullscreen b_alpha=0]
	[grp bg="black" sys_menu=false b_alpha=0]

	[pop_stack clear=true]
	[jump fn=main label=*title]

;*********************************************************
*game_end
	[ask_ync mes="ゲームを終了しますか？"]
		;#FONT	ゲームを終了しますか？
	[jump label=*retry cond="_yesno=='n'"]

	[lay layer=mes page=fore visible=&v]
	[jump fn=main label=*game_end_do]

;*********************************************************
*exit
	[frame id=submenu visible=false]
	[sys_menu visible=&sysmenu_v]
	[lay layer=mes page=fore visible=&v]
[return]

;*********************************************************
*cancel
	[se fn=&sysse_cancel buf=SYS]
	[frame id=submenu visible=false]
	[sys_menu visible=&sysmenu_v]
	[lay layer=mes page=fore visible=&v]
	[wait time=200]		; パフォーマンス改善

	[enable_event enabled=true]
	[let name="isGameState" text=true]
[return]
