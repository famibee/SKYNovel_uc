	[call fn=setting]
	[call fn=ext_*]
	[call fn=sub]
	[call fn=_yesno]

	[toggle_full_screen key=w]
	[event global=true call=true key=alt+enter label=*toggle_full_screen]
		&s = const.sn.config.window.height > const.sn.screenResolutionY || const.sn.config.window.width > const.sn.screenResolutionX
		[toggle_full_screen cond='! const.sn.displayState && s']
		; プレイ環境より画面サイズが大きい場合は全画面モードに
	[event global=true call=true key=ArrowLeft label=*set_focus arg=prev]
	[event global=true call=true key=ArrowRight label=*set_focus arg=next]
	[event global=true call=true key=p label=*snapshot]
	[event global=true call=true key=F5 label=*reload_script cond=const.sn.isDebugger]
	[event global=true call=true key=F7 label=*stats cond=const.sn.isDebugger]
	[event global=true call=true key=F8 label=*dump cond=const.sn.isDebugger]
	[event global=true call=true key=Meta+0 label=*reset_size cond=const.sn.isApp]
;	[update_check url='http://ugainovel.hiho.jp/famibee/sample/hatsune/' cond=!const.体験版]

[macro name=lr nowarn_unused=true]
	[ws buf=VOICE canskip=true stop=false cond='sn.auto.enabled && !sn.skip.enabled']
	[wq]
	[l]
	;[r]
[endmacro]
[char2macro char=@ name=lr]
[char2macro char=\ name=plc]

	; *** レジューム処理 ***
	[sys_resume_load cond=!const.sn.key.escape]

	[jump fn=title]


;********************************************************
*close
	[enable_event enabled=false]
	[ask_ync mes=&mes.ゲームを終了しますか？]
	[enable_event enabled=true]
*close_do
	[return cond="_yesno=='n'"]
*game_end_do
	[enable_event enabled=false]
	[wait time=500]
[close]

;********************************************************
*toggle_full_screen
	[let name=txt text=&mes.ウインドウモード]
	[let name=txt text=&mes.全画面モード cond=!const.sn.displayState]
	[toggle_full_screen]
	[notice text=&txt]

	; 設定画面変更
	[return cond='!const._config.inited']
	[set_frame id=config var_name=val_chkScrMode text=&const.sn.displayState]
;	[let_frame id=config var_name=val2ctrl function=true]
[return]

*set_focus
	[set_focus to=&sn.eventArg]
[return]

*snapshot
	[snapshot]
	[notice text=&mes.スクリーンショットをダウンロードしました]
[return]

*reload_script
	[return cond=!isGameState]
	[reload_script]

*stats
	[event global=true del=true key=f7]
;;	[stats]
	[notice text='F7 test']
[return]

*dump
	[dump_lay]
;	[dump_val]
;	[dump_stack]
[return]

*reset_size
	[window width=&const.sn.config.window.width height=&const.sn.config.window.height]
[return]
