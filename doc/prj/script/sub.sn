;********************************************************
;	定数
;********************************************************
; 各種効果音を指定
&sysse_ok1 = 'BurstB_11'
&sysse_ok2 = 'BellA_16'
&sysse_ok2_long = 'BellB_11'
&sysse_cancel = 'bell05'
&sysse_choice = 'wood04'

; メッセージウインドウにボタンを配置するか（システムメニュー）
&useSysMenu = true

; デフォルトフォント
&def_fonts = 'ipamjm, QuiMi_mincho'
;&sn.button.fontFamily = 'ipamjm'

; 立ち絵配置位置定数
;	[lay pos=&pos.l	等として使用。
;	組み込みで「c」という指定が出来る。例：pos=c、pos='c'
;	これを指定すると画面中央。（pos、leftを省略するとpos=c扱い）
&pos.l = 140
&pos.c = const.sn.config.window.width /2
&pos.r = const.sn.config.window.width -pos.l
&pos.lc = (pos.c + pos.l) /2
&pos.rc = (pos.r + pos.c) /2

; 「桜の樹の下には」特有の立ち絵配置位置定数
&pos.vleft_disp_x = 40 + 280
&pos.vleft_disp_w = const.sn.config.window.width - pos.vleft_disp_x

&pos.l1c = pos.vleft_disp_x + pos.vleft_disp_w * (1/2)

&pos.l2l = pos.vleft_disp_x + pos.vleft_disp_w * (1/4)
&pos.l2r = pos.vleft_disp_x + pos.vleft_disp_w * (3/4)

; 表示されるレイヤ（セーブ・スナップショットなどの対象）
&dsp_lays_grp = 'base,0,1,2'	; 画像
&dsp_lays = dsp_lays_grp +',mes'	; 文字
[let name=dsp_lays text="&dsp_lays_grp +',mes,mes_sysmenu'" cond=useSysMenu]

;********************************************************
;	各種初期値
;********************************************************
[if exp=const.sn.isFirstBoot]	; ゲームがインストールされ、初めての起動
	&sys:TextLayer.Back.Alpha = 0.7
		; バック不透明度。テキストウインドウの背景の濃度
[endif]

;********************************************************
; マクロ タイトル開始共通処理（main.snで呼ぶ）
;********************************************************
[macro name=sys_title_start]
	&lastVoice = ''
	&isGameState = false
	&sys:doResume = false
	&save:sn.doRecLog = false

	[txt_lay_fullscreen b_alpha=0 onend=leave no_sysmenu_draw=true]
	[er]	; ページ両面の文字消去＆改ページ
		; （改ページしないと履歴がページからあふれる）
	[set_focus to=null]
	[fadeoutse buf=SE time=500]
	[sys_menu visible=false]
	[clear_event]
[endmacro]

;********************************************************
; マクロ シナリオ開始時共通処理（ロード時にもコール）
;********************************************************
[macro name=sys_scenario_start]
	&lastVoice = ''
	&isGameState = true
	&save:sn.doRecLog = true

	[fadeoutbgm time=300]
	[fadeoutse buf=SE time=500]

	; テキストレイヤ全画面設定
	[txt_lay_fullscreen b_alpha=0 no_sysmenu_draw=true]
	[lay layer=mes page=back style="&'font-family: #{def_fonts};'"]
	[grp bg=black sys_menu=false b_alpha=0 time=0]

	[record_place layer=&dsp_lays]
		[sys_resume_save cond="! sys:doResume && ! mp:no_resume_save"]
		&sys:doResume = true

[jump label=*game_inited cond='const.game.inited']
&const.game.inited = true

	[call fn=extG_*]	; extG_*.snというスクリプトを全部コールする
	[call fn=_hidetext]
	[call fn=_submenu]
	[call fn=_archive]
	[call fn=_log]
	[call fn=_config]
	[call fn=_album]
	[call fn=face*]		; face**.snというスクリプトを全部コール

	[event global=true call=true key=a label=*toggle_auto_read]
	[event global=true call=true key=f label=*toggle_skip]

*game_inited
	[enable_event enabled=true]
	[set_focus to=null]
[endmacro]

;********************************************************
; マクロ レジューム処理
[macro name=sys_resume_load]
	[return cond=!sys:doResume]

	[ask_ync mes='前回終了ポイントから再開しますか？' cond='! const.sn.isDbg']
	[return cond="_yesno=='n'"]

	[jump fn=_archive label=*do_load_resume]
[endmacro]

;********************************************************
; マクロ レジューム退避処理
[macro name=sys_resume_save]
	[call fn=_archive label=*do_save_resume]
[endmacro]


;********************************************************
; マクロ システムメニュー描画処理（縦書き）
[macro name=sysmenu_draw_v]
	[return cond=!useSysMenu]
	[sys_menu visible=false]
	[clear_lay layer=mes_sysmenu page=both]
	[return cond="mp:no_sysmenu_draw != null"]

	[let name=l text=%l|40]
	[let name=w text=%w|292]
	[let name=sysmenu_left text=&l+12]
	[let name=sysmenu_left2 text=&sysmenu_left+w+13]
	;&r = 0	; ボタンを横にしたいなら
	&r = 90	; ボタンを縦にしたいなら

	[lay layer=mes_sysmenu page=back]
	[button layer=mes_sysmenu page=back text='クイックセーブ' rotation=&r left=&sysmenu_left2 top=600 global=true event_at_down=true fn=_archive label=*quick_save call=true hint='クイックセーブ' enterse=&sysse_choice]
	[button layer=mes_sysmenu page=back text='クイックロード' rotation=&r left=&sysmenu_left2 top=720 global=true event_at_down=true fn=_archive label=*quick_load call=true hint='クイックロード' enterse=&sysse_choice]

	[button layer=mes_sysmenu page=back text='タイトルへ' rotation=&r left=&sysmenu_left top=220 global=true event_at_down=true fn=sub label=*title_sys call=true hint='タイトルへ' enterse=&sysse_choice]
		; どの本文スクリプトから呼ばれるか不明なので、
		; 同一スクリプトへのコールだがfn属性を省略してはいけない

	[button layer=mes_sysmenu page=back text=' セーブ ' rotation=&r left=&sysmenu_left top=320 global=true event_at_down=true fn=_archive label=*rclick_save call=true hint='セーブ' enterse=&sysse_choice]
	[button layer=mes_sysmenu page=back text=' ロード ' rotation=&r left=&sysmenu_left top=420 global=true event_at_down=true fn=_archive label=*rclick_load call=true hint='ロード' enterse=&sysse_choice]
	[button layer=mes_sysmenu page=back text='字を隠す' rotation=&r left=&sysmenu_left top=520 global=true event_at_down=true fn=_hidetext label=*main call=true hint='文字非表示' enterse=&sysse_choice]
	[button layer=mes_sysmenu page=back text=' 履 歴 ' rotation=&r left=&sysmenu_left top=620 global=true event_at_down=true fn=_log label=*main call=true hint='履歴を表示' enterse=&sysse_choice]
	[button layer=mes_sysmenu page=back text=' 設 定 ' rotation=&r left=&sysmenu_left top=720 global=true event_at_down=true fn=_config label=*keycall_config call=true hint='ゲーム設定' enterse=&sysse_choice]
	[trans layer=mes_sysmenu time=0]
	[wt]	; 裏から表へコピー（save対策）
[endmacro]

;-----------------------------
; マクロ システムメニュー表示切り替え
[macro name=sys_menu]
	[lay layer=mes_sysmenu visible=%visible|true cond=useSysMenu]
[endmacro]

[add_lay layer=mes_sysmenu class=txt cond=useSysMenu]
[let name=sysmenu_left text=NaN]

;********************************************************
; マクロ テキストレイヤ縦書き左設定
[macro name=txt_lay_v_left]
	[let name=l text=%l|40]
	[let name=w text=%w|310]
	&pl = 26
	&pt = 42
	[let name=pr text=%pr|36]
	&pb = 42
	[let name=fcol text=%fcol|'white']
	; こちらの素材をお借りしました。
	; びたちー素材館　メッセージボックス・メッセージウィンドウ・フレーム　フリー素材 http://www.vita-chi.net/message.htm
	[lay * layer=%layer|'mes' page=%page|back visible=%visible|true left=&l top=0
		b_pic=%b_pic|wafuu1	; 背後に画像を使う（width・heightは画像に合わされる）
	;	b_color=%b_color|'#ffffff'	; 背後に画像を使わない・単色塗り
		; その際はstyleにサイズ指定必須 width: #{w}px; height: #{const.sn.config.window.height}px;
		b_alpha=%b_alpha|1
	;	b_alpha_isfixed=true	; コメント外すと透過度変更禁止
		style="&'color: #{fcol}; writing-mode: vertical-rl; padding-left: #{pl}px; padding-right: #{pr}px; padding-top: #{pt}px; padding-bottom: #{pb}px; font-family: #{def_fonts}; font-size: 24px; line-height: 1.5;'"]
	[sysmenu_draw_v *]
[endmacro]

;********************************************************
; マクロ テキストレイヤ縦書き中設定
[macro name=txt_lay_v_center]
	[txt_lay_v_left * l=%l|366]
[endmacro]

;********************************************************
; マクロ テキストレイヤ縦書きWide中設定
[macro name=txt_lay_v_center_wide nowarn_unused=true]
	[txt_lay_v_center * l=%l|294 w=436 max_row=11 pr=26
	b_pic=wafuu1w	; 背後に画像を使わない場合はコメントアウト
]
[endmacro]


;********************************************************
; マクロ システムメニュー描画処理（横書き）
[macro name=sysmenu_draw_h]
	[return cond=!useSysMenu]
	[sys_menu visible=false]
	[clear_lay layer=mes_sysmenu page=both]
	[return cond="mp:no_sysmenu_draw != null"]

	[let name=sysmenu_left text=200]
	[let name=sysmenu_top text=&const.sn.lay.mes.back.y+mp:h-10]
	&r = 0	; ボタンを横にしたいなら
	;&r = 90	; ボタンを縦にしたいなら

	[lay layer=mes_sysmenu page=back]
	[button layer=mes_sysmenu page=back text='タイトルへ' rotation=&r left=&sysmenu_left top=&sysmenu_top global=true event_at_down=true fn=sub label=*title_sys call=true hint='タイトルへ' enterse=&sysse_choice]
		; どの本文スクリプトから呼ばれるか不明なので、
		; 同一スクリプトへのコールだがfn属性を省略してはいけない

	[button layer=mes_sysmenu page=back text=' セーブ ' rotation=&r left=&sysmenu_left+100 top=&sysmenu_top global=true event_at_down=true fn=_archive label=*rclick_save call=true hint='セーブ' enterse=&sysse_choice]
	[button layer=mes_sysmenu page=back text=' ロード ' rotation=&r left=&sysmenu_left+200 top=&sysmenu_top global=true event_at_down=true fn=_archive label=*rclick_load call=true hint='ロード' enterse=&sysse_choice]
	[button layer=mes_sysmenu page=back text='字を隠す' rotation=&r left=&sysmenu_left+300 top=&sysmenu_top global=true event_at_down=true fn=_hidetext label=*main call=true hint='文字非表示' enterse=&sysse_choice]
	[button layer=mes_sysmenu page=back text=' 履 歴 ' rotation=&r left=&sysmenu_left+400 top=&sysmenu_top global=true event_at_down=true fn=_log label=*main call=true hint='履歴を表示' enterse=&sysse_choice]
	[button layer=mes_sysmenu page=back text=' 設 定 ' rotation=&r left=&sysmenu_left+500 top=&sysmenu_top global=true event_at_down=true fn=_config label=*keycall_config call=true hint='ゲーム設定' enterse=&sysse_choice]
	[button layer=mes_sysmenu page=back text='クイックセーブ' rotation=&r left=&sysmenu_left+600 top=&sysmenu_top global=true event_at_down=true fn=_archive label=*quick_save call=true hint='クイックセーブ' enterse=&sysse_choice]
	[button layer=mes_sysmenu page=back text='クイックロード' rotation=&r left=&sysmenu_left+700 top=&sysmenu_top global=true event_at_down=true fn=_archive label=*quick_load call=true hint='クイックロード' enterse=&sysse_choice]

	[trans layer=mes_sysmenu time=0]
	[wt]	; 裏から表へコピー（save対策）
[endmacro]

;********************************************************
; マクロ テキストレイヤ全画面設定
[macro name=txt_lay_fullscreen]
	[let name=t text=%t|40]
	[let name=h text=%h|'&const.sn.config.window.height -40*2']
	&pl = 70
	&pt = 30
	&pr = 70
	&pb = 70
	;[let name=fcol text=%fcol|'white']
	[let name=fcol text=%fcol|'black']
	[lay * layer=%layer|'mes' page=%page|back visible=%visible|true left=0 top=&t
	;	b_pic=%b_pic|wafuu1	; 背後に画像を使う（width・heightは画像に合わされる）
		b_color=%b_color|0xffffff	; 背後に画像を使わない・単色塗り
		; その際はstyleにサイズ指定必須
		b_alpha=%b_alpha|1
	;	b_alpha_isfixed=true	; コメント外すと透過度変更禁止
		style="&'color: #{fcol}; width: #{const.sn.config.window.width}px; height: #{h}px; writing-mode: horizontal-tb; padding-left: #{pl}px; padding-right: #{pr}px; padding-top: #{pt}px; padding-bottom: #{pb}px; font-family: #{def_fonts}; font-size: 24px; line-height: 1.5;'"]
	[sysmenu_draw_h * h=&h]
[endmacro]

;********************************************************
; マクロ テキストレイヤ横書きWide下設定
[macro name=txt_lay_h_bottom_wide nowarn_unused=true]
	[txt_lay_fullscreen * t=%t|472 h=266 max_row=6]
[endmacro]

;********************************************************
; マクロ ＢＧＭ切り替え
[macro name=bgm]
	[stopbgm cond='mp:time == null || sn.skip.enabled']

	[let name=t text=%time|500]	;省略した時の初期値
	[stopse buf=BGM_FO]
	[xchgbuf buf=BGM buf2=BGM_FO]

	[if exp='mp:time == null || sn.skip.enabled']
		[playbgm *]
	[else]
		[fadeoutse buf=BGM_FO time=&t]
		[playbgm * volume=0]
		[fadebgm * volume=%volume|1 time=&t]
	[endif]
[endmacro]

;********************************************************
; マクロ 効果音
[macro name=se]
	[stopse * cond='mp:fn == null']
	[playse * cond='mp:fn != null' join=false]
[endmacro]

;********************************************************
; マクロ 改ページ
[macro name=plc]
	[ws buf=音声 canskip=true cond='sn.auto.enabled && !sn.skip.enabled']
	[p visible=%visible|true]
	[er]
	[record_place layer=&dsp_lays]
		[sys_resume_save]
	; [sys_resume_save]後に何を書いても、ロード時は無視される。（ここを通らない）
	; _archive.sn の【ロード後最初にしたい共通処理】*do_load_initに書く。
	&lastVoice = ''
	[fadeoutse buf=SE time=%time|500 cond='mp:no_se_stop === null && !sn.skip.enabled']
	[stopse buf=SE cond='sn.skip.enabled']
	[stopse buf=%buf|音声 cond='mp:no_voice_stop === null || sn.skip.enabled']
[endmacro]

;********************************************************
; マクロ 選択肢直後にすべき処理
[macro name=after_choice nowarn_unused=true]
	;[pop_stack]
	[record_place layer=&dsp_lays]
		[sys_resume_save]
[endmacro]

;********************************************************
; マクロ 場面転換
;********************************************************
[macro name=grp]
	[let name=t text=%t_time|300]	;省略した時の初期値
	[let name=t text=0 cond="sn.skip.enabled"]
	[let name=tt text=%time|1000]	;省略した時の初期値
	[let name=tt text=0 cond="sn.skip.enabled"]

	; テキストOff
	[if exp='mp:nofo_txt == null']
		[sys_menu visible=false]
		[lay layer=mes page=back visible=false]
		[trans layer=mes time=&t]
		[wt]
		[er rec_page_break=false]
	[endif]

	; 効果音
	[se fn=%se cond='mp:se != null']

	; レイヤ切り替え・メイン
	[lay layer=base page=back fn=%bg scale_x=%sxb|null face=%fb|null visible=true cond='mp:bg != null']
	[img layer=0 fn=%l0|null pos=%pos0|null left=%left0|null top=%top0|null alpha=%o0|null scale_x=%sx0|null face=%f0|null]
	[img layer=1 fn=%l1|null pos=%pos1|null left=%left1|null top=%top1|null alpha=%o1|null scale_x=%sx1|null face=%f1|null]
	[img layer=2 fn=%l2|null pos=%pos2|null left=%left2|null top=%top2|null alpha=%o2|null scale_x=%sx2|null face=%f2|null]
	[trans * layer=&dsp_lays_grp time=&tt]
	[wt]

	; テキストOn
	[if exp='mp:nofi_txt == null']
		[lay layer=mes page=back visible=true b_alpha=%b_alpha|1]
		[trans layer=mes time=&t]
		[wt]
		[sys_menu visible=%sys_menu|true]
	[endif]
[endmacro]

;********************************************************
; マクロ 文字数分のウェイトを入れる
[macro name=wc nowarn_unused=true]
	[wait cond='mp:time != null && sys:sn.tagCh.doWait && !sn.skip.enabled' time='&mp:time * sys:sn.tagCh.msecWait']
[endmacro]



;********************************************************
[return]

;********************************************************
*toggle_auto_read
	&tmp:sn.auto.enabled = true
	[set_cancel_skip]
	[notice text='自動読み進みOn']
	[return]

*toggle_skip
	&tmp:sn.skip.enabled = true
	[set_cancel_skip]
	[notice text='強制スキップ On']
	[return]

;********************************************************
*title_sys
	[enable_event enabled=false]
	[enable_event layer=mes_sysmenu enabled=false]
	[ask_ync mes='タイトルに戻りますか？']
		;#FONT	タイトルに戻りますか？
	[enable_event enabled=true]
	[enable_event layer=mes_sysmenu enabled=true]
	[return cond="_yesno=='n'"]

	[fadeoutse buf=音声 time=1000]
	; テキストレイヤ全画面設定
	[txt_lay_fullscreen b_alpha=0]
	[grp bg='black' sys_menu=false b_alpha=0]

	[pop_stack clear=true]
	[jump fn='main' label=*title]
